plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id "com.diffplug.spotless" version "6.25.0"
}

group = 'com.tagtrace'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(22)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring boot dependencies
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// Database
	runtimeOnly 'com.h2database:h2'
	// Other dependencies
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	implementation 'commons-validator:commons-validator:1.9.0'
	implementation 'io.nayuki:qrcodegen:1.8.0'
	implementation 'org.mapstruct:mapstruct:1.6.2'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'

	// Test
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
	java {
		palantirJavaFormat()
		removeUnusedImports()
		importOrder()
		formatAnnotations()
		indentWithSpaces(4)
		trimTrailingWhitespace()
		endWithNewline()
	}
}

build {
	dependsOn spotlessCheck
}

tasks.register('updatePreCommitHook', Copy) {
	from './scripts/pre-commit'
	into './.git/hooks'
}
compileJava.dependsOn updatePreCommitHook

tasks.named('test') {
	useJUnitPlatform()
}